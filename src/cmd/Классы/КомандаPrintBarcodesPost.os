#Использовать "../../core"

Процедура ОписаниеКоманды(Знач КомандаПриложения) Экспорт
	
	КомандаПриложения.Опция("i id", "", "UUID заказа");
	КомандаПриложения.Опция("c cdek", "", "Номер отслеживания");
	КомандаПриложения.Опция("f format", "A4", "Формат этикетки");
	КомандаПриложения.Опция("l lang", "RUS", "Язык формирования этикетки (RUS, ENG))");

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач КомандаПриложения) Экспорт

	Если СДЭК.ЗащитаОтИзменений() Тогда
		ВызватьИсключение НСтр("ru='Команда не доступна в защищенном режме!';");
	КонецЕсли;

	УИДЗаказа = КомандаПриложения.ЗначениеОпции("id");
	НомерОтслеживания = КомандаПриложения.ЗначениеОпции("cdek");
	Формат = КомандаПриложения.ЗначениеОпции("format");
	Язык = КомандаПриложения.ЗначениеОпции("lang");

	СтруктураЗаказа = Новый Структура;
	АдресРесурса = "POST /print/barcodes";

	Если ЗначениеЗаполнено(УИДЗаказа) Тогда
		СтруктураЗаказа.Вставить("order_uuid", УИДЗаказа);
	ИначеЕсли ЗначениеЗаполнено(НомерОтслеживания) Тогда
		СтруктураЗаказа.Вставить("cdek_number", НомерОтслеживания);
	Иначе
		ВызватьИсключение НСтр("ru='Не указан номер заказа!';");
	КонецЕсли;

	Заказы = Новый Массив;
	Заказы.Добавить(СтруктураЗаказа);

	СтруктураЗапроса = Новый Структура;
	СтруктураЗапроса.Вставить("orders", Заказы);
	СтруктураЗапроса.Вставить("format", Формат);
	СтруктураЗапроса.Вставить("lang", Язык);

	Ответ = СДЭК.ВыполнитьЗапросСДЭК(АдресРесурса,
		СтруктураЗапроса,
		СДЭК.ПараметрыУчетнойЗаписиСДЭК());

	ОбщегоНазначенияСДЭК.ВывестиОтвет(Ответ);

КонецПроцедуры

